#!/usr/bin/perl
# Compares two KML files and finds placemarks in the first file that don't have any matches
# within a specified radius (default 1 km) in the second file. Useful for finding locations
# that are unique to the first file.
# Usage: find-kml-uniques [-r radius] file1.kml file2.kml

use strict;
use warnings;
use utf8;
use XML::Simple qw(:strict);
use Getopt::Long qw(GetOptions);

sub extractPlacemarks {
    my @results = ();
    my $xml = XMLin(shift(), ForceArray => 0, KeyAttr => [ 'id' ]);
    my @documents = $xml->{Document};
    foreach my $document (@documents) {
	# Process placemarks within folders
	my @folders = $document->{Folder};
	foreach my $folder (@folders) {
	    if (ref $folder ne 'ARRAY') {
		$folder = [ $folder ];
	    }
	    foreach my $f (@{$folder}) {
		my $s1 = $f->{name} // '';
		chomp($s1);
		my @placemarks = $f->{Placemark};
		foreach my $placemark (@placemarks) {
		    if (ref $placemark ne 'ARRAY') {
			$placemark = [ $placemark ];
		    }
		    foreach my $p (@{$placemark}) {
			my $s2 = $p->{name} // '';
			chomp($s2);
			my $name = $s1 . '/' . $s2;
			my $coordinates = $p->{Point}->{coordinates};
			next unless defined $coordinates;
			$coordinates =~ s/^\s+|\s+$//g;
			my ($longitude, $latitude, $altitude) = split(',', $coordinates);
			next unless defined $longitude && defined $latitude;
			push(@results, [ $name, $longitude, $latitude ]);
		    }
		}
	    }
	}
	# Process placemarks directly under document
	if ($document->{Placemark}) {
	    my @placemarks = $document->{Placemark};
	    foreach my $placemark (@placemarks) {
		if (ref $placemark ne 'ARRAY') {
		    $placemark = [ $placemark ];
		}
		foreach my $p (@{$placemark}) {
		    my $s2 = $p->{name} // '';
		    chomp($s2);
		    my $coordinates = $p->{Point}->{coordinates};
		    next unless defined $coordinates;
		    $coordinates =~ s/^\s+|\s+$//g;
		    my ($longitude, $latitude, $altitude) = split(',', $coordinates);
		    next unless defined $longitude && defined $latitude;
		    push(@results, [ $s2, $longitude, $latitude ]);
		}
	    }
	}
    }
    return @results;
}

sub computeDistance {
  my $lon1 = shift();
  my $lat1 = shift();
  my $lon2 = shift();
  my $lat2 = shift();
  my $dLat = deg2rad($lat2-$lat1);
  my $dLon = deg2rad($lon2-$lon1); 
  my $a = sin($dLat/2.0) * sin($dLat/2.0) + cos(deg2rad($lat1)) * cos(deg2rad($lat2)) * sin($dLon/2.0) * sin($dLon/2.0); 
  return 12742.0 * atan2(sqrt($a), sqrt(1-$a));
}

sub deg2rad {
  return shift() * (3.14159265359 / 180.0)
}

Getopt::Long::Configure qw(gnu_getopt);

my $radius = 1;
GetOptions('radius|r=f' => \$radius) or die "Usage: $0 [-r radius] file1.kml file2.kml\n";

die "Usage: $0 [-r radius] file1.kml file2.kml\n" unless @ARGV == 2;

my @many = extractPlacemarks($ARGV[0]);
my @few = extractPlacemarks($ARGV[1]);

binmode STDOUT, ":encoding(UTF-8)";

MAIN: for (my $i = 0; $i < scalar(@many); $i++) {
    for (my $j = 0; $j < scalar(@few); $j++) {
	my $distance = computeDistance($many[$i][1], $many[$i][2], $few[$j][1], $few[$j][2]);
	if ($distance <= $radius) {
	    next MAIN;
	}
    }
    print $many[$i]->[0] . "\n";
}
